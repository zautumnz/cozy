# keai's built-in http server is significantly faster than node with express,
# with a similar API, at least for simple examples.
# ab shows roughly 5000 rps for express, and 14000 for keai.

let app = http.server()
app.static("./")
# route pattern can be a simple string
app.route("/foo", fn (req) {
    # implicit return example
    { "body": "in get foo" }
})
# or a regex
app.route("^/bar$", ["POST"], fn (req) {
    return { "body": "posted!" }
})
app.route("^/quux$", ["GET", "POST"], fn (req) {
    let content_type = "application/json"
    if req.method == "GET" {
        return {
            "body": json.serialize({ "got": "quux" }),
            "content_type": content_type,
        }
    } else {
        if req.content_type == "application/json" {
            return { "body": req.body, "content_type": content_type }
        } else {
            return { "status_code": 403 }
        }
    }
})
app.log()
# this is equivalent to:
# app.use(fn (req) { print(json.serialize(req)) })
# regex is necessary to capture params
app.route("/test/(.*)", fn (req) { {"body": req.params.join("\n")} })
app.route("/form", ["POST"], fn (req) {
    # req.form will be a hash of input_name (string) to input value (string)
    print(req.form)
    # req.files will be a hash of input name (string) to array of hashes;
    # the file hashes will be original file name (string) and
    # a keai file object (stored in the os tempdir)
    print(req.files)
    return { "status_code": 201 }
})
# because http.server() includes a core.event_emitter(), we can
# emit custom events
app.emit("foo", "bar")
# app.error subscribes the callback to the error event.
# this is identitcal to `app.on("error", cb)`
app.error(fn (e) { print("oh no!", e) })
app.listen(8000, fn (x) { print("listening on", x) })
